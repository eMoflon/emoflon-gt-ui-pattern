grammar org.moflon.gt.mosl.pattern.language.MOSLPattern with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate moslPattern "http://www.moflon.org/gt/mosl/pattern/language/MOSLPattern"


GraphTransformationPatternFile: 
	imports += Import*
	//using += Using*	'package' name = QualifiedName	
	modules += PatternModule*
;

PatternModule:'module' name = ID '{'
	patterns += PatternDef*
	'}'
;

PatternDef:'pattern' name=ID '(' (parameters += PatternParameter)? (',' parameters += PatternParameter)* ')' '{'	
	variables += NACAndObjectVariable*
	'}'
;

NACAndObjectVariable:NACGroup | ObjectVariablePattern;

ObjectVariablePattern:(op = Operator)? name = ID ':' type = [ecore::EClass | QualifiedName] (':=' assignment=[ObjectVariablePattern | QualifiedName])? ('{' 
		attributeAssignments += AttributeAssignment*
		attributeConstraints += AttributeConstraint*
		linkVariablePatterns += LinkVariablePattern*	
'}')?;

NACGroup:{NACGroup}	'nac' (name =ID)? '{'
	objects+=ObjectVariablePattern*
	'}'
;

PatternParameter:ObjectVariablePattern;


AttributeConstraint:
	attribute = [ecore::EAttribute] op=('==' | '!=' | '<=' | '>=' | '>' | '<') valueExp = Expression
;

AttributeAssignment:
	attribute = [ecore::EAttribute] op=':=' valueExp = Expression
;

Expression:
	(LiteralExpression | AttributeExpression | EnumExpression)
;

EnumExpression:
	 'enum::' eenum=[ecore::EEnum | QualifiedName] '::' literal = [ecore::EEnumLiteral | QualifiedName]  
;

AttributeExpression:
	objectVar = [ObjectVariablePattern] '.' attribute = [ecore::EAttribute]
;

LiteralExpression:
	value = LiteralValue
;

LinkVariablePattern:
	(op = Operator)? '-' type = [ecore::EReference] '->' target = [ObjectVariablePattern]
;

Operator:
	value = OPERATOR_VALUE
;


/* ---------------- */
/*    Auxiliaries   */
/* ---------------- */



Import:
	'import' name = STRING
;

Using:
	'using' importedNamespace = QualifiedNameWithWildcard
;

terminal OPERATOR_VALUE: '++'| '!' | '--';

terminal DECIMAL: INT '.' INT;

LiteralValue:
	(STRING | ('-')?INT | ('-')?DECIMAL | BOOL)
;

terminal BOOL:
	'true' | 'false'
;

QualifiedNameWithWildcard:
	QualifiedName '.*'
;

QualifiedName returns ecore::EString:
	ID ('.' ID)*
;


// additional declarations for typing

AbstractAttribute: AttributeConstraint | AttributeAssignment;

PatternObject: PatternVariable | Expression;

PatternVariable:NACAndObjectVariable | LinkVariablePattern;



//ObjectVariableDefinition: ;

